PARSER_BEGIN(Parser)

package lkh.expression.parser;

import lkh.expression.Expression;
import java.util.List;
import java.util.LinkedList;

public class Parser {}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  < IMPLIES : "implies" >
  | < OR : "or" >
  | < AND : "and" >
  | < NOT : "not" >
  | < KH : "kh" >
  | < ID : ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >
}

Expression Expression() :
{ Expression t; }
{
    t=Implies() <EOF>
    { return t; }
}

Expression Implies() :
{ Expression left, right; }
{
    left=Disyunction()
    (
        <IMPLIES> right=Implies()
        { left = new Expression(TokenType.IMPLIES, "implies", left, right); }
    )?
    { return left; }
}

Expression Disyunction() :
{ Expression left, right; }
{
    left=Conjunction()

    (<OR> right=Conjunction()
    { left = new Expression(TokenType.OR, "or", left, right); }
    )*

    {return left;}
}

Expression Conjunction() :
{ Expression left, right; }
{
    left=Not()

    (<AND> right=Not()
    { left = new Expression(TokenType.AND, "and", left, right); }
    )*

    {return left;}
}

Expression Not() :
{ Expression t; }
{
    <NOT> t=Atom()
    { return new Expression(TokenType.NOT, "not", null, t); }

    | t=Atom()
    { return t; }
}

Expression Atom() :
{
    Token t;
    List<String> l = new LinkedList<>();
    Expression left, right;
}
{
    t=<ID> ("(" ArgsList(l) ")")?
    { 
        if (l.isEmpty())
            return new Expression(TokenType.PROP, t.image);
            
        return new Expression(TokenType.PROP, t.image + "(" + String.join(", ", l) + ")");
    }

    | "(" left=Implies() ")"
    { return left; }

    | <KH> "(" left=Implies() "," right=Implies() ")"
    { return new Expression(TokenType.KH, "kh", left, right); }
}

void ArgsList(List<String> argsList) :
{ Token t; }
{
    t=<ID> { argsList.add(t.image); } ("," ArgsList(argsList))?
}
